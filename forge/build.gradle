plugins {
    id 'com.gradleup.shadow'
}

loom {
    forge {
        mixinConfig "totemfactory.mixins.json"
    }
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    //Dependencies
    forge "net.minecraftforge:forge:$rootProject.forge_version"
    modImplementation "com.simibubi.create:create-1.20.1:$rootProject.create_forge:all"
    modImplementation "com.tterrag.registrate:Registrate:$rootProject.registrate"

    //Mods for testing
    modRuntimeOnly "mezz.jei:jei-1.20.1-forge:${rootProject.jei_version}"

    //Shit Forge wants so it works
    include(implementation("io.github.llamalad7:mixinextras-forge:$rootProject.mixinextras_version"))
    modImplementation "net.createmod.ponder:Ponder-Forge-1.20.1:$rootProject.ponder"
    modImplementation "dev.engine-room.flywheel:flywheel-forge-1.20.1:$rootProject.flywheel"

    //Architectury
    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionForge')
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/mods.toml') {
        expand version: inputs.properties.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}
